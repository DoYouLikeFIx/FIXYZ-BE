plugins {
  id 'org.springframework.boot'
  id 'io.spring.dependency-management'
  id 'org.springdoc.openapi-gradle-plugin'
}

def springdocPort = 18080

openApi {
  apiDocsUrl = "http://localhost:${springdocPort}/v3/api-docs"
  outputDir = file("$buildDir/openapi")
  outputFileName = "${project.name}.json"
  waitTimeInSeconds = 30
  customBootRun {
    args = ["--spring.profiles.active=openapi"]
  }
}

tasks.named('forkedSpringBootRun') {
  dependsOn(':core-common:jar', ':channel-domain:jar')
}

dependencies {
  implementation project(':core-common')
  implementation project(':channel-domain')

  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-actuator'
  implementation 'org.springframework.boot:spring-boot-starter-validation'
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
  implementation 'org.flywaydb:flyway-core'
  implementation 'org.flywaydb:flyway-mysql'
  implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.15'

  runtimeOnly 'com.mysql:mysql-connector-j'
  runtimeOnly 'com.h2database:h2'

  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation platform("org.testcontainers:testcontainers-bom:${rootProject.ext.testcontainersVersion}")
  testImplementation 'org.testcontainers:junit-jupiter'
  testImplementation 'org.testcontainers:testcontainers'
  testImplementation 'org.testcontainers:mysql'
  testImplementation "org.wiremock:wiremock-standalone:${rootProject.ext.wiremockVersion}"
}
